// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    apply from: 'versions.gradle'
    repositories {
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.0-alpha14'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
    }
}

plugins {
    id 'com.diffplug.spotless' version '5.11.1'
}

subprojects {
    repositories {
        google()
        mavenCentral()
        maven { url 'https://jitpack.io' }
        jcenter()// exoplayer still in jcenter
    }

    apply plugin: 'com.diffplug.spotless'
    spotless {
        kotlin {
            target '**/*.kt'
            targetExclude("$buildDir/**/*.kt")
            targetExclude('bin/**/*.kt')

            ktlint(versions.ktlint)//.userData([android: "true"])
            licenseHeaderFile rootProject.file('spotless/license.kt')
        }
        java {
            target '**/*.java'
            targetExclude("$buildDir/**/*.java")
            targetExclude('bin/**/*.java')

            licenseHeaderFile rootProject.file('spotless/license.kt')
        }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            allWarningsAsErrors = true
            freeCompilerArgs = [
                    '-Xallow-unstable-dependencies',
                    '-Xopt-in=kotlin.RequiresOptIn',
            ]
            jvmTarget = "11"
        }
    }
}